/*                                                                      */
/*  Copyright 2015 SRI International                                    */
/*  License: https://ncanda.sri.com/software-license.txt                */
/*                                                                      */
/*  $Revision: 2120 $                                                   */
/*  $LastChangedBy: nicholsn $                                          */
/*  $LastChangedDate: 2015-08-07 14:23:35 -0700 (Fri, 07 Aug 2015) $    */
/*                                                                      */
********************************************************************;
  *************************************************************;
  ***** DSMIV  MARIJUANA DEPENDENCE **********;
  *************************************************************;
********************************************************************;

label   mjd4dpsx  ='# DSM4 MJ Criteria Endorsed'
        mjd4dpdx ='DSM4 MJ Dependence'
        mjd4dpao ='Age of Onset for DSM4 Alcohol Dep.'
        mjd4abdx ='DSM4 MJ abuse'
        mjd4absx   ='# of sx,DSM4 marijuana abuse'
        mjd4d1='DSM4 marijuana tolerance'
        mjd4d2='DSM4 withdrawal'
        ;


keep mjd4dpsx mjd4dpdx mjd4d1 mjd4d2 mjd4dpao;
 keep mjd4abdx mjd4absx;
mjd4dpdx=1;
********************************************************;
***   D1           tolerance       ***;
mjd4d1=1;
if mj9=5 then mjd4d1=5;
else
   if mj9=.K then mjd4d1=9;

************************************;

********************************************************;
********  D2  withdrawal;
************************************;
mjd4d2=1;
if (mj10b=5) | (mj10c=5) then mjd4d2=5;
else
   if (mj10b=.K) | (mj10c=.K) then mjd4d2=9;

********************************************************;
***D3  substance often taken in larger amounts over longer
      period of time than intended;
************************************;
mjd4d3=1;
if mj8=5 then mjd4d3=5;
else
   if mj8=.K then mjd4d3=9;

********************************************************;
*** D4  persistent desire or unsuccessful efforts to cut down
      or control;
************************************;
mjd4d4=1;
if (mj7=5) | (mj7b=5) then mjd4d4=5;
else
   if (mj7=.K) | (mj7b=.K) then mjd4d4=9;

********************************************************;
**** D5  a great deal of time spent...(obtaining,using,recovering);
************************************;
mjd4d5=1;
if mj5=5 then mjd4d5=5;
else
   if mj5=.K then mjd4d5=9;
************************************;

********************************************************;
**** D6 important ... activities given up or reduced ..;
************************************;
mjd4d6=1;
if mj17a=5 then mjd4d6=5;
else
   if mj17a=.K then mjd4d6=9;


********************************************************;
*** D7 continued use despite knowledge of ..physical or psychological
          problem;
************************************;
mjd4d7=1;
cnt5=0;
cnt9=0;

array mj6col2_ (4) mj6a1-mj6a3 mj6a5;
do i=1 to 4;
   if mj6col2_ (i)=5 then cnt5+1;
   else
      if mj6col2_ (i)=.K then cnt9+1;
end;
if cnt5>0 then mjd4d7=5;
else
   if cnt9>0 then mjd4d7=9;


************************************;

******************************************************;
**** count criteria endorsed;
cnt5=0;
cnt9=0;
array mjd4d_(7) mjd4d1-mjd4d7;
do i=1 to 7;
   if mjd4d_(i)=5 then cnt5+1;
   else
      if mjd4d_(i)=.K then cnt9+1;
   end;
mjd4dpsx=cnt5;

******************************************************;
**********CLUSTERING WIHTIN 12 MONTH PERIOD **********;
      **MJ6a1YrCl, MJ6a2YrCl combined insingle clustering question;

MJD4Cluster= (MJ5YrCl=5)+
             ((MJ6a1YrCl)|(MJ6a3YrCl)|(MJ6a5YrCl=5))+
              ((MJ7YrCl=5)|((MJ7aYRCl=5)*(Mj7b=5)))+
              (MJ8YrCl=5)+(MJ9YrCl=5)+
              ((MJ10bYrCl=5)|(MJ10cYrCl=5))+
              (MJ17aYrCl=5);



************ DEPENDENCE *****************************;
***** 3 or more criteria + clustering within 12 month period?;
******************************************************;
if (cnt5>=3)*(MJD4Cluster>=3) then mjd4dpdx=5;
else
   if (sum(cnt5,cnt9)>=3) then mjd4dpdx=9;
   

if mjd4dpdx=5 then do;
   mjd4dpao=MJ19cAgeOns;
end;


******************************************************;
 ***** DSMIV ABUSE *****;
 mjd4abdx=1;

 ****A1  failure to fulfill major role obligations ...;
mjd4aba1=1;
if mj14=5 then mjd4aba1=5;
else
   if mj14=.K then mjd4aba1=9;

**** A2 recurrent use in physically hazardous situations;
mjd4aba2=1;
if (mj11a1=5) | (mj11c1=5) then  mjd4aba2=5;
else
   if (mj11a1=.K) | (mj11c1=.K) then  mjd4aba2=9;


***** A3 recurrent substance-related legal problems;
mjd4aba3=1;
if mj13a1=5 then mjd4aba3=5;
else
   if mj13a1=9 then mjd4aba3=9;

***** A4  continued use despite...social or interpersonal problems;
mjd4aba4=1;
if (mj6b=5) | (mj12a=5) then mjd4aba4=5;
else
   if (mj6b=.K) | (mj12a=.K) then mjd4aba4=9;

***********  ANY CRITERION ENDORSED?;
mjd4aba=1;
cnt5=0;
cnt9=0;
array mjd4aba_(4) mjd4aba1-mjd4aba4;
do i=1 to 4;
   if mjd4aba_(i)=5 then cnt5+1;
   else
      if mjd4aba_(i)=9 then cnt9+1;
end;
if cnt5>0 then mjd4aba=5;
else
   if cnt9>0 then mjd4aba=9;
mjd4absx=cnt5;

******** met criteria for dependence? ;
if (mjd4dpdx=1) * (mjd4aba=5) then mjd4abdx=5;
else
   if (mjd4dpdx in(1,9)) * (mjd4aba in (5,9)) then mjd4abdx=9;



********************************************************;
  *** set all variables to missing if section skipped;
array mjd4array (*) mjd4dpsx mjd4dpdx mjd4d1 mjd4d2 mjd4dpao
                    mjd4abdx mjd4absx;


if (mj1 in (.K,.R,.)) then do;
    do i=1 to dim(mjd4array);
        mjd4array(i)=.;
    end;
end;

********************************************************;