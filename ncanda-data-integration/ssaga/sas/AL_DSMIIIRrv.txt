/*                                                                      */
/*  Copyright 2015 SRI International                                    */
/*  License: https://ncanda.sri.com/software-license.txt                */
/*                                                                      */
/*  $Revision: 2119 $                                                   */
/*  $LastChangedBy: nicholsn $                                          */
/*  $LastChangedDate: 2015-08-07 14:18:11 -0700 (Fri, 07 Aug 2015) $    */
/*                                                                      */
 *******************************************************************;
/*******************************************/
/* ALCOHOL DEPENDENCE &  ABUSE,  DSM3R    */
/*******************************************/
/************************************/
/* ALCOHOL DEPENDENCE, CRITERION A */
/************************************/

keep aldrdpdx aldrdpa aldrdpb aldrdpsx
     aldrabdx aldraba1 aldraba2;
label aldrdpdx='alcohol dependence, dsm3r';
label aldrdpa='alcohol dependence, dsm3r criterion a';
label aldrdpb='alcohol dependence, dsm3r criterion b';
label aldrabdx='alcohol abuse, dsm3r';
label aldrdpsx='num sx,dsm3r alcohol dependence';
*label aldrdpao='age of onset, dsm3r alcohol dependence';
label aldraba1='alcohol abuse, dsm3r, criterion a1';
label aldraba2='alcohol abuse, dsm3r, criterion a2';
aldrdpdx=1;
aldrabdx=1;

aldrdpa=1;
array aldrdpa_(9) aldrdpa1-aldrdpa9;
do i=1 to 9;
   aldrdpa_(i)=1;
end;

**********************************;
**** A1.Substance taken in larger amounts or over a longer period ***;
if (al12c=5) | (al13b=5) then aldrdpa1=5;
else
   if (al12c in(.K )) | (al13b in(.K )) then aldrdpa1=9;

**********************************;
***  A2. Persistent desire or unsuccessful efforts to cut down or control **;
if (al10=5) | (al10c=1) then aldrdpa2=5;
else
   if (al10 in(.K )) | (al10c in(.K )) then aldrdpa2=9;

**********************************;
*** A3. A great deal of time spent ....;
if (al15a=5) then aldrdpa3=5;
else
   if (al15a in(.K )) then aldrdpa3=9;

**********************************;
*** A4. Frequent intoxication or withdrawal dx when expected to fulfill
      major obligations ...;
if ((al16b>=3) or (al16b1=5)) | (al21=5) | ((al22b>=3)|(al22b1=5))
   | ((al23b>=3)|(al23b1=5)) | ((al24b>=3)|(al24b1=5)) | (al25=5)
   | ((al29b>=3)|(al29b1=5)) then aldrdpa4=5;
else
   if (al16b1 in(.K )) | (al21 in(.K )) |(al22b1 in(.K ))
      | (al23b1 in(.K )) | (al24b1 in(.K )) | (al25 in(.K ))
      | (al29b1 in(.K )) then aldrdpa4=9;

**********************************;
** A5. Important social, occupational, or recreational activities
       given up or reduced...;
if al14b=5 then aldrdpa5=5;
else
   if al14b in(.K ) then aldrdpa5=9;

**********************************;
*** A6. Continued substance use despite .. social, psychological
        or physical problem ...;
if (al26b1=5) | (al26b2=5) | (al26b3=5) | (al27c=5) |
   ((al28b>=3)|(al28b1=5)) | (al31b=5) | (al32=5) | (al33a=5)
   then aldrdpa6=5;
else
   if (al26b1 in(.K )) | (al26b2 in(.K )) | (al26b3 in(.K )) | (al27c in(.K )) |
      (al28b1 in(.K )) | (al31b in(.K )) | (al32 in(.K )) | (al33a in(.K ))
      then aldrdpa6=9;

**********************************;
*** A7.  Marked tolerance ......;
if (al9d=5) | (al9i=5) then aldrdpa7=5;
else
   if (al9d in(.K )) | (al9i in(.K )) then aldrdpa7=9;

**********************************;
*** A8. Characteristic withdrawal sx;
cnt5=0;
cnt9=0;
array al37a(9);  **** in ssaga2, had been al37_a(9);
cnt37a_5=0;
cnt37a_9=0;
do i=2 to 9;
   if al37a(i)=5 then do;
      cnt37a_5+1;
   end;
   if al37a(i) in(.K ) then do;
      cnt37a_9+1;
   end;
end;
if (al37a1=5)*(cnt37a_5>0) then cnt5+1;
else
   if (al37a1 in(.K )) | (cnt37a_9>0) then cnt9+1;
if al38=5 then cnt5+1;
else
   if al38 in(.K ) then cnt9+1;
if al39=5 then cnt5+1;
else
   if al39 in(.K ) then cnt9+1;
if cnt5>0 then aldrdpa8=5;
else
   if cnt9>0 then aldrdpa8=9;


**********************************;
*** A9. Substance often taken to relieve or avoid withdrawal sx;
if (al37i=5) | (al38c=5)| (al39c=5) then aldrdpa9=5;
else
   if (al37i in(.K )) | (al38c in(.K ))| (al39c in(.K )) then aldrdpa9=9;

    ****** count ******;
cnta5=0;
cnta9=0;
do i=1 to 9;
  if aldrdpa_(i)=5 then cnta5+1;
  if aldrdpa_(i)=9 then cnta9+1;
end;
if cnta5>=3 then aldrdpa=5;
else
   if sum(cnta5,cnta9)>=3 then aldrdpa=9;

aldrdpsx=cnta5;

 ********** DSMIIIR DEPENDENCE CRITERION B *******************;
**** persistence (at least 1 month) or recurrence;
 array chk_ary (7) aldrdpa1 aldrdpa3 aldrdpa4 aldrdpa5
     aldrdpa6 aldrdpa7 aldrdpa9;
 aldrdpb=1;
 AlMnthCluster=((AL9dMnthCL=5)|(AL9iMnthCL=5))+
              ((AL10MnthCL=5)|(AL10cMnthCL=5))+
			  ((AL12cMnthCl=5)|(Al13bMnthCl=5))+
			  (AL14bMnthCl=5) + (AL15aMnthCL=5)+
              ((AL16bMnthCL=5)|((AL21MnthCl=5)*(AL21c=5))| 
               (AL22bMnthCl=5)|(AL23bMnthCl=5)|(AL24bMnthCl=5)|
               (Al25MnthCl=5)|(AL29bMnthCl=5))+
              ((AL26a1MnthCl=5)|(AL26a2MnthCl=5)|(AL26a3MnthCl=5)|
               (AL27cMnthCl=5)|(AL28bMnthCl=5)|(AL31bMnthCl=5)|(AL32MnthCl=5)|
               (AL33aMnthCl=5))+
			  ((AL37dMnthCl=5)|(AL38MnthCl=5)|(AL39cMnthCl=5))+
               ((AL37iMnthCl=5)|(AL38cMnthCL=5)|(AL39cMnthcl=5));
 
 if AlMnthCluster>=3 then aldrdpb=5;
 else do;
    cntb5=0;
    cntb9=0;
    do i=1 to 7;
       if chk_ary(i)=5 then cntb5+1;
       else
          if chk_ary(i)=9 then cntb9+1;
    end;
    if (al10=5) | (al10d>=3) | (al10d1=5) then cntb5+1;
    else
       if (al10 in(.K )) | (al10d1 in(.K )) then cntb9+1;
    if ((al37a1=5)*(cnt37a_5>0)) 
    * ((al37b>=30) | (al37f>=3) | (al37f1=5))
       then cntb5+1;
     else
       if (al37a1 in(.K )) | (cnt37a_9>0) |
        (al37b in(.K )) | (al37f1 in(.K ))
           then cntb9+1;
   if (al38b>=3) | (al38b1=5) then cntb5+1;
   else
      if (al38b1 in(.K )) then cntb9+1;
    if (al39b>=3) | (al39b1=5) then cntb5+1;
    else
       if (al39b1 in(.K )) then cntb9+1;

    if cntb5>=3 then aldrdpb=5;
    else
       if (sum(cntb5,cntb9)>=3)  then aldrdpb=9;
end;

  ********** dependence: both criteria? ****************;
 if (aldrdpa=5)*(aldrdpb=5) then aldrdpdx=5;
 else
    if (aldrdpa in(5,9))*(aldrdpb in(5,9)) then aldrdpdx=9;
  ******************************************;

***********DSMIII-R ALCOHOL ABUSE *****************;
aldrab=1;

**** A1 continued use despite knowledge of ...problems...;
aldraba1=1;
cnt5a1= (al26b1=5) + (al26b2=5) +  (al26b3=5) 
 +  ((al28b>=3) | (al28b1=5)) + (al31b=5) + (al32=5) + (al33a=5)
   + (al27c=5);
cnt9a1= (al26b1 in(.K )) + (al26b2 in(.K )) +  (al26b3 in(.K )) 
 +  (al28b1 in(.K )) + (al31b in(.K )) + (al32 in(.K )) + (al33a in(.K ))
   + (al27c in(.K ));
if cnt5a1>0 then aldraba1=5;
else
   if cnt9a1>0 then aldraba1=9;
****A2 recurrent use in physically hazardous situations;
aldraba2=1;
cnt5a2=(al21=5)+ ((al22b>=3) | (al22b1=5)) + ((al23b>=3) | (al23b1=5))
   + ((al24b>=3) | (al24b1=5)) + ((al29b>=3) | (al29b1=5));
cnt9a2= (al21 in(.K )) + (al22b1 in(.K )) + (al23b1 in(.K ))
      + (al24b1 in(.K )) + (al29b1=.K);
if cnt5a2>0  then aldraba2=5;
else
    if cnt9a2>0 then aldraba2=9;

 ********** DSMIIIR ABUSE, CRITERION B *******************;
**** persistence (at least 1 month) or recurrence;
aldrabb=1;
if sum(cnt5a1,cnt5a2)>=2 then aldrabb=5;
else
   if sum(cnt5a1,cnt5a2,cnt9a1,cnt9a2)>=2 then aldrabb=9;


***** CRITERION C: has subject met criteria for dependence?;
****  abuse can be positive if dependence is unknown;
if (aldrdpdx in(1,9)) * ((aldraba1=5)|(aldraba2=5))*(aldrabb=5)
   then aldrabdx=5;
else
   if (aldrdpdx in(1,9)) *((aldraba1 in(5,9))|(aldraba2 in(5,9)))
      *(aldrabb in(5,9)) 
      then aldrabdx=9;

   ****((aldraba1=9)|(aldraba2=9)|(aldrabb=9)) ******;
     **** cut on Mar2, *(aldrabb in(1,9)) changed to *(aldrabb in(5,9)) ;
********************************************************;
  *** set all variables to missing if section skipped;
array ald3rarray (*) aldrdpdx aldrdpa aldrdpb aldrdpsx
                      aldrabdx aldraba1 aldraba2;

if (al1 in (.,.K ))*(al1a in (.,.K )) then do;
    do i=1 to dim(ALd3rarray);
        ALd3rarray(i)=.;
    end;
end;

*********************************************************;
