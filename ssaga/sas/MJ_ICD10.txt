/*                                                                      */
/*  Copyright 2015 SRI International                                    */
/*  License: https://ncanda.sri.com/software-license.txt                */
/*                                                                      */
/*  $Revision$                                                   */
/*  $LastChangedBy$                                            */
/*  $LastChangedDate$    */
/*                                                                      */
************************************************;
**** ICD-10 Marijuana Dependence ;
************************************************;
keep mjcddpdx mjcddpsx mjcddpao mjcddpa  mjicd10cluster;
label mjcddpdx ='ICD10 marijuana dependence'
      mjcddpsx='# ICD10 marijuana criteria endorsed'
      mjcddpao='Age of onset, ICD10 marijuana dependence'
      mjcddpa='ICD10, criterion A'
      mjicd10cluster='MJ Icd10 Clustering ciriterion'
    ;

**********************************************************;
****** craving;
mjcddpd1=1;
if mj16=5 then mjcddpd1=5;
else
   if mj16=9 then mjcddpd1=9;

**********************************************************;
**** loss of control or persistent desire to cut down;
mjcddpd2=1;
if (mj7=5) | (mj7b=5) | (mj8=5) then mjcddpd2=5;
else
   if (mj7=9) | (mj7b=9) | (mj8=9) then mjcddpd2=9;


**********************************************************;
*** withdrawal state or
    use of substance to relieve or avoid withdrawal;
mjcddpd3=1;
if (mj10b=5) | (mj10c=5) then mjcddpd3=5;
else
   if  (mj10b=9) | (mj10c=9) then mjcddpd3=9;

*************************************************************;
****** tolerance;
 mjcddpd4=1;
if mj9=5 then mjcddpd4=5;
else
   if mj9=9 then mjcddpd4=9;


*************************************************************;
****** neglect of alternative pleasures, behaviors or interests...,
        great deal of time spent...;
mjcddpd5=1;
if (mj5=5) | (mj17a=5) then mjcddpd5=5;
else
   if (mj5=9) | (mj17a=9) then mjcddpd5=9;


***************************************************************;
*** continued use despite harmful consequences;
mjcddpd6=1;
cnt5=0;
cnt9=0;
array mjcd6_(5) mj6a_1-mj6a_3 mj6a_5 mj11c;
do i=1 to 5;
   if mjcd6_(i)=5 then cnt5+1;
   else
      if mjcd6_(i)=9 then cnt9+1;
end;
if cnt5>0 then mjcddpd6=5;
else
   if cnt9>0 then mjcddpd6=9;

***********  how many criteria endorsed?;
mjcddpdx=1;
cnt5=0;
cnt9=0;
array mjcddp_(6) mjcddpd1-mjcddpd6;
do i=1 to 6;
   if mjcddp_(i)=5 then cnt5+1;
   else
      if mjcddp_(i)=9 then cnt9+1;
end;
if cnt5>=3 then mjcddpa=5;
else
  if sum(cnt5,cnt9)>=3 then mjcddpa=9;

***** clustering?  ; ***on interview, clustering questions for MJ6a1,MJ6a2 are combined;
MJICD10Cluster=((MJ6a1YrCl=5)|(MJ6a3YrCl=5)|(Mj6a5YrCl=5)|((MJ11cYrCl=5)*(MJ11c1=5)))+
               ((MJ7YrCl=5)|((MJ7aYrCl=5)*(MJ7b=5))|(MJ8YrCl=5))+
               (MJ9YrCl=5)+
               ((MJ10bYrCl=5)|(MJ10cYrCl=5))+
               ((Mj5YrCl=5)|(Mj17aYrCl=5))+
               (MJ16YrCl=5);


 ***** ICD10? (3 or more criteria + clustering);
if (mjcddpa=5)*(MJICD10Cluster>=3) then mjcddpdx=5;
else
   if mjcddpa=9 then mjcddpdx=9;
mjcddpsx=cnt5;
if mjcddpdx=5 then do;
   mjcddpao=mj19AgeOns;
end;

****************************************************************************;
*********** set variables to missing if section skipped ********************;
array mjICD10array (*) mjcddpdx mjcddpsx mjcddpao mjcddpa  mjicd10cluster;
if mj1 in(.K,.R,.) then do;
   do i= 1 to 5;
     mjicd10array(i)=.;
   end;
end;
*****************************************************************************;
